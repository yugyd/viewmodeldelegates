plugins {
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.androidKmpLibrary)
    alias(libs.plugins.android.lint)
    alias(libs.plugins.maven.publish)
}

group = ARTIFACT_GROUP
version = ARTIFACT_VERSION

kotlin {
    androidLibrary {
        namespace = "com.yugyd.viewmodeldelegates.ui"
        compileSdk = 36
        minSdk = 24
    }

    iosX64()
    iosArm64()
    iosSimulatorArm64()

    sourceSets {
        commonMain {
            dependencies {
                implementation(project(":viewmodeldelegates"))

                // Kotlin
                implementation(libs.kotlin.stdlib)
                implementation(libs.coroutines)

                // Lifecycle
                implementation libs.androidx.lifecycle.viewmodel
            }
        }

        commonTest {
            dependencies {
                implementation(libs.kotlin.test)
            }
        }
    }
}

mavenPublishing {
    publishToMavenCentral(true, false)
    signAllPublications()

    pom {
        name.set(POM_NAME)
        description.set(POM_DESCRIPTION)
        url.set(POM_URL)

        licenses {
            license {
                name.set(POM_LICENSE_NAME)
                url.set(POM_LICENSE_URL)
                distribution.set(POM_LICENSE_DIST)
            }
        }

        scm {
            url.set(POM_SCM_URL)
            connection.set(POM_SCM_CONNECTION)
            developerConnection.set(POM_SCM_DEV_CONNECTION)
        }

        developers {
            developer {
                id.set(POM_DEVELOPER_ID)
                name.set(POM_DEVELOPER_NAME)
                email.set(POM_DEVELOPER_EMAIL)
            }
        }
    }
}
